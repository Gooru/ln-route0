/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java Library project to get you started.
 * For more details take a look at the Java Libraries chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.4/userguide/java_library_plugin.html
 */

plugins {
    id 'java' // or 'groovy' Must be explicitly applied
    id "com.github.johnrengelman.shadow" version "2.0.2"
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    mavenCentral()
    maven {
        url = 'http://oss.sonatype.org/content/repositories/snapshots/'
    }
}

defaultTasks 'shadowJar'

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException('''Build needs Java 8. You are using something else, try again.'''.stripMargin())
}


ext {
    vertxVersion = '3.5.0'
    slf4jVersion = '1.7.12'
    junitVersion = '4.12'
    logbackVersion = '1.0.13'
    hikariVersion = '2.7.6'
    postgresVersion = '42.2.2'
    jdbiVersion = '2.78'
}

dependencies {
    // Vertx specific dependencies
    compile "io.vertx:vertx-core:${vertxVersion}"
    compile "io.vertx:vertx-hazelcast:${vertxVersion}"
    compile "io.vertx:vertx-web:${vertxVersion}"
    compile "io.vertx:vertx-dropwizard-metrics:${vertxVersion}"

    // Other dependencies
    compile "ch.qos.logback:logback-classic:${logbackVersion}"
    compile "com.zaxxer:HikariCP:${hikariVersion}"
    compile "org.postgresql:postgresql:${postgresVersion}"
    compile "io.vertx:vertx-redis-client:${vertxVersion}"
    compile "org.jdbi:jdbi:${jdbiVersion}"
    // Test dependencies
    testCompile "junit:junit:${junitVersion}"

}

version = "0.1-snapshot"

tasks.withType(JavaCompile) {
    //enable compilation in a separate daemon process
    options.fork = true
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    //enable incremental compilation
    options.incremental = true
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Class': 'org.gooru.route0.bootstrap.AppRunner'
        attributes 'Created-By': 'Gooru.org'
    }
}

defaultTasks 'shadowJar'

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}
